plugins {
    id 'pmd'
    id 'checkstyle'
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
}

def checkstyleVersion = '8.+'
def pmdVersion = '6.21.0'

group = 'com.nugurang'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '14'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    checkstyleConfig
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:29.+'
    implementation 'com.graphql-java:graphiql-spring-boot-starter:5.+'
    implementation 'com.graphql-java:graphql-java:14.+'
    implementation 'com.graphql-java:graphql-java-extended-scalars:1.+'
    implementation 'com.graphql-java:graphql-java-spring-boot-starter-webmvc:1.+'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    checkstyleConfig("com.puppycrawl.tools:checkstyle:${checkstyleVersion}") {
        transitive = false
    }
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = checkstyleVersion
    //config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'sun_checks.xml')
    //configFile = project(':').file("config/checkstyle/checkstyle.xml")
    //configProperties = [ "suppressionFile" : project(':').file('config/checkstyle/suppressions.xml')]
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

checkstyleTest {
    enabled = false
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

pmd {
    toolVersion = pmdVersion
    consoleOutput = true
    ignoreFailures = true
    reportsDir = file("${buildDir}/pmd-output")
}
 
tasks.withType(Pmd) {
    reports {
        xml.destination = file("${pmd.reportsDir}/pmd-report.xml")
        html.enabled = false
        xml.enabled = true
    }
}